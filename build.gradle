buildscript {
	dependencies {
		classpath "com.modrinth.minotaur:Minotaur:${libs.versions.minotaur.get()}"
		classpath "com.github.breadmoirai:github-release:${libs.versions.githubRelease.get()}"
	}
}

plugins {
	id 'maven-publish'
	alias libs.plugins.loom
}

version = "$project.baseVersion+$project.branch"
archivesBaseName = project.name
group = 'folk.sisby'

repositories {
	mavenLocal()
	maven { url "https://maven.proxyfox.dev/"
	}
	maven { url 'https://repo.sleeping.town'}
	maven { url "https://maven.terraformersmc.com/"
	}
	maven { url 'https://maven.ladysnake.org/releases'}
	maven { url "https://maven.nucleoid.xyz/" }
	maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
	maven { url 'https://maven.cafeteria.dev'}
	maven { url "https://maven.jamieswhiteshirt.com/libs-release" }
	maven { url "https://maven.shedaniel.me/"}
	maven { url "https://maven.terraformersmc.com/"}
	maven { url 'https://maven.wispforest.io'}
	maven {
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		url 'https://jitpack.io'
		content {
			includeGroupByRegex 'com\\.github\\..*'
		}
	}
}

dependencies {
	// // Base // //
	minecraft libs.mc
	mappings variantOf(libs.yarn) { classifier "v2" }
	modImplementation libs.fl
	modImplementation libs.fapi

	modImplementation libs.switchyCore
	modImplementation libs.switchyClient
	modImplementation libs.switchyUI
	modImplementation libs.switchyCompat
	modImplementation libs.placeholder
	modImplementation libs.styledChat
}

loom {
	accessWidenerPath = file("src/main/resources/switchy-proxy.accesswidener")
}

processResources {
	final Map<String, String> map = [
			"version": version.toString(),
			"mc"  : libs.versions.mc.get(),
			"fl"  : libs.versions.fl.get(),
			"fapi"  : libs.versions.fapi.get(),
			"switchy": libs.versions.switchy.get(),
			"styledChat": libs.versions.styledChat.get()
	]

	inputs.properties(map)
	filesMatching('*.mod.json') { expand(map) }
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE.txt") {
		rename { "${it}_${archivesBaseName}" }
	}
	from("src/main/resources/data/switchy_proxy/lang/en_us.json") {
		into { "assets/switchy_proxy_data/lang" }
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
		maven {
			name "ProxyFox"
			url "https://maven.proxyfox.dev/"
			credentials(PasswordCredentials)
		}
	}
}

if (System.getenv("MODRINTH_TOKEN")) {
	apply plugin: "com.modrinth.minotaur"
	modrinth {
		token = System.getenv("MODRINTH_TOKEN")
		projectId = 'switchy-proxy'
		versionNumber = project.version
		versionName = baseVersion
		versionType = 'release'
		uploadFile = remapJar
		gameVersions = [libs.versions.mc.get()]
		loaders = ['quilt', 'fabric']
		detectLoaders = false
		dependencies {
			required.project "switchy"
			required.project "styled-chat"
		}
		changelog = "Changelog: https://github.com/sisby-folk/switchy-proxy/releases/tag/v" + baseVersion
		syncBodyFrom = rootProject.file("README.md").text
	}
}

if (System.getenv("GITHUB_TOKEN")) {
	apply plugin: "com.github.breadmoirai.github-release"
	githubRelease {
		token System.getenv("GITHUB_TOKEN")
		owner "sisby-folk"
		repo "switchy-proxy"
		tagName 'v' + baseVersion
		releaseName baseVersion
		targetCommitish project.defaultBranch
		draft false
		prerelease false
		releaseAssets remapJar.archiveFile
		allowUploadToExisting true
		generateReleaseNotes true
	}
	tasks.githubRelease.dependsOn remapJar
}

if (System.getenv("MODRINTH_TOKEN") && System.getenv("GITHUB_TOKEN")) {
	tasks.register("fullRelease") {
		group "publishing"
		dependsOn "githubRelease"
		dependsOn "modrinth"
	}
}
